apply from: 'buildsystem/dependencies.gradle'

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
    }
}

configure(project(":configuration") ) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    sourceCompatibility = JavaVersion.VERSION_1_8    
}

configure(allprojects){
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = JavaVersion.VERSION_1_8
}

subprojects {

    configurations {
        cucumberRuntime {
            extendsFrom testRuntime
        }
    }
    
    dependencies {
		testCompile group: 'org.springframework.boot', name: 'spring-boot-test', version: '2.1.6.RELEASE'
		testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    jacoco {
        toolVersion = "0.8.1"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                includes = ['co.com.ceiba.restservice.lendings*']
                limit {
                    minimum = 0.5
                }
            }

            rule {
                enabled = true
                element = 'CLASS'
                includes = ['co.com.ceiba.restservice.lendings.*']

                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 100
                }
            }
        }
    }

    task cucumber() {
        dependsOn assemble, compileTestJava
        doLast {
            javaexec {
                main = "cucumber.api.cli.Main"
                classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
                args = ['--plugin', 'pretty', '--glue', 'co.com.ceiba.restservice.lendings', 'src/test/resources']
            }
        }
    }
           
}
